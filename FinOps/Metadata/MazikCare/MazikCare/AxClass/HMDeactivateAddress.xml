<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HMDeactivateAddress</Name>
	<SourceCode>
		<Declaration><![CDATA[
class HMDeactivateAddress
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Called by the menu items.
    /// </summary>
    /// <param name="_args">
    ///    The <c>Args</c> object passed from the menu item.
    /// </param>
    public static void main(Args _args)
    {
        DirPartyPostalAddressView   partyPostalAddress;
        boolean                     isDefault;
        FormRun                     callerFormRun;
        FormObjectSet               formObjectSet;
        Common                      callerRecord;
        FormRun                     formRun;
        Object                      formRunObject;
    
        // Verify context
        if (_args.record() == null || _args.record().TableId != tablenum(DirPartyPostalAddressView))
        {
            // Class %1 must be called with buffer %2.
            throw error(strfmt("@SYS113735", classstr(DirPartyPostalAddressAction), tablestr(DirPartyPostalAddressView)));
        }
    
        formObjectSet = FormDataUtil::getFormDataSource(_args.record());
        if (formObjectSet)
        {
            callerFormRun = formObjectSet.formRun();
    
            if(_args.parm() == 'Organization')
            {
                formObjectSet = callerFormRun.dataSource('DirOrganization');
            }
            else
            {
                formObjectSet = callerFormRun.dataSource('CustTable');
            }
            callerRecord = formObjectSet.cursor();
        }
    
        partyPostalAddress = _args.record();
    
        if(LogisticsLocationDefault::implementsDefaultLocations(callerRecord) &&
            LogisticsLocationDefault::isAddressDefault(partyPostalAddress.PostalAddress, callerRecord))
        {
            isDefault = true;
        }
    
        if (_args.parmEnumType() == enumnum(LogisticsLocationAddressActionButtons))
        {
            switch (_args.parmEnum())
            {
                case LogisticsLocationAddressActionButtons::Deactivate :
                    if (isDefault)
                    {
                        if (Box::yesNo("@SYS344641", DialogButton::No) == DialogButton::Yes)
                        {
                            partyPostalAddress.deactivate(callerRecord, isDefault);
                        }
                    }
                    else
                    {
                        partyPostalAddress.deactivate(callerRecord);
                    }
                    break;
            }
    
            formRun = _args.caller().owner();
            if(formHasMethod(formRun,identifierstr(refreshAddressGrid)))
            {
                formRunObject = formRun;
                formRunObject.refreshAddressGrid();
            }
    
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>