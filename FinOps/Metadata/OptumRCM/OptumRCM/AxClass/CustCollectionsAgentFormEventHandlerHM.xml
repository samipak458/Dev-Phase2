<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustCollectionsAgentFormEventHandlerHM</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class which implements supervisor lookup logic.
/// </summary>
class CustCollectionsAgentFormEventHandlerHM
{    
}

]]></Declaration>
		<Methods>
			<Method>
				<Name>CustCollectionsAgent_HMSupervisor_OnLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Implement the HMSupervisor field lookup logic.
    /// </summary>
    /// <param name="sender">FormControl</param>
    /// <param name="e">FormControlEventArgs</param>
    [FormControlEventHandler(formControlStr(CustCollectionsAgent, CustCollectionsAgent_HMSupervisor), FormControlEventType::Lookup)]
    public static void CustCollectionsAgent_HMSupervisor_OnLookup(FormControl sender, FormControlEventArgs e)
    {
        CustCollectionsAgent    custCollectionsAgent;
        SysReferenceTableLookup sysTableLookup;
        Query                   query = new Query();
        QueryBuildDataSource    qbdsDirPartyTable, qbdsDirPartyRelationship, qbdsCustCollectionsAgent;

        FormDatasource formDatasource = sender.formRun().datasource(tableStr(CustCollectionsAgent));
        custCollectionsAgent = formDatasource.cursor();

        qbdsDirPartyTable = query.addDataSource(tableNum(DirPartyTable));
        qbdsDirPartyRelationship = qbdsDirPartyTable.addDataSource(tableNum(DirPartyRelationship));
        qbdsDirPartyRelationship.joinMode(JoinMode::InnerJoin);
        qbdsDirPartyRelationship.addLink(fieldNum(DirPartyTable,RecId),fieldNum(DirPartyRelationship,ChildParty));
        qbdsCustCollectionsAgent = qbdsDirPartyRelationship.addDataSource(tableNum(CustCollectionsAgent));
        qbdsCustCollectionsAgent.joinMode(JoinMode::InnerJoin);
        qbdsCustCollectionsAgent.addLink(fieldNum(DirPartyRelationship,RecId),fieldNum(CustCollectionsAgent,DirPartyRelationship));
        qbdsCustCollectionsAgent.addRange(fieldNum(CustCollectionsAgent, RecId)).value(SysQuery::valueNot(custCollectionsAgent.RecId));

        sysTableLookup = SysReferenceTableLookup::newParameters(tableNum(DirPartyTable), sender);

        sysTableLookup.parmQuery(query);
        sysTableLookup.addLookupfield(fieldNum(DirPartyTable, Name));

        sysTableLookup.performFormLookup();

        FormControlCancelableSuperEventArgs cancelableSuperEventArgs = e as FormControlCancelableSuperEventArgs;
        cancelableSuperEventArgs.CancelSuperCall();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>