<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HMAssignPoolToZeroClaimMultiThreadBatchJob</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is used as the main batch job class for assignment of pool batch job for $0 denied claims
/// </summary>
class HMAssignPoolToZeroClaimMultiThreadBatchJob extends RunBaseBatch implements BatchRetryable
{
    QueryRun                 gQueryRun;
    BatchHeader              batchHeader;
    HMNumberOfThreads        numberOfThreads;
    HMClaimJobType           jobType, jobTypeDataPrep;
    RecId                    hmClaimBatchJobLogsHeader, hmClaimBatchJobLogsHeaderDataPrep;

    #OCCRetryCount
    #define.CurrentVersion(1)
    #localmacro.CurrentList
        numberOfThreads
    #endmacro
    #LOCALMACRO.HMClaimBatchJobLogsHeader
        hmClaimBatchJobLogsHeader
    #ENDMACRO
    #LOCALMACRO.HMClaimBatchJobLogsHeaderDataPrep
        hmClaimBatchJobLogsHeaderDataPrep
    #ENDMACRO
    #LOCALMACRO.JobType
        jobType
    #ENDMACRO
    #localmacro.JobTypeDataPrep
        jobTypeDataPrep
    #endmacro

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Construct the class
    /// </summary>
    /// <returns>Return the class object</returns>
    public server static HMAssignPoolToZeroClaimMultiThreadBatchJob construct()
    {
        return new HMAssignPoolToZeroClaimMultiThreadBatchJob();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Description of the task
    /// </summary>
    /// <returns>Return description of the task</returns>
    static ClassDescription description()
    {
        return "@Optum:CalculateZeroClaimPoolMultipleThreadBatchJobs";
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method initiated the task for for assignment of pool batch job for $0 denied claims
    /// </summary>
    public void run()
    {
        HMDataPrepForCalcZeroClaimPoolMultiThreadBatchJob   hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob;
        HMCalculateZeroClaimPoolMultiThreadBatchJob         hmCalculateZeroClaimPoolMultiThreadBatchJob;
        HMUpdateZeroClaimBatchJobLogsHeaderCount            hmUpdateZeroClaimBatchJobLogsHeaderCount;
        int                                                 recordsToProcess, perThreadRecordsRound, additionalRecordsRound;
        HMOneDecimal                                        additionalRecords;
        real                                                perThreadRecords, diffPerThread;
        RecId                                               batchJobId;
        HMClaimQualifiedForPoolAssignment                   hmClaimQualifiedForPoolAssignment;
        HMClaimPaymentPostingLatestDenialCode               hmClaimPaymentPostingLatestDenialCode;

        try
        {
            delete_from hmClaimPaymentPostingLatestDenialCode
                where hmClaimPaymentPostingLatestDenialCode.CreatedDateTime <= DateTimeUtil::addDays(DateTimeUtil::utcNow(), -2);
        }
        catch (Exception::Deadlock)
        {
            if (xSession::currentRetryCount() >= #RetryNum)
            {
                throw error(strFmt("@MAZ:FailedToDeleteDataFromTmpTable", 'HMClaimPaymentPostingLatestDenialCode'));
            }
            else
            {
                retry;
            }
        }
        catch
        {
            if (xSession::currentRetryCount() >= #RetryNum)
            {
                throw error(strFmt("@MAZ:FailedToDeleteDataFromTmpTable", 'HMClaimPaymentPostingLatestDenialCode'));
            }
            else
            {
                retry;
            }
        }

        try
        {
            delete_from hmClaimQualifiedForPoolAssignment
                where hmClaimQualifiedForPoolAssignment.CreatedDateTime <= DateTimeUtil::addDays(DateTimeUtil::utcNow(), -2);
        }
        catch (Exception::Deadlock)
        {
            if (xSession::currentRetryCount() >= #RetryNum)
            {
                throw error(strFmt("@MAZ:FailedToDeleteDataFromTmpTable", 'HMClaimQualifiedForPoolAssignment'));
            }
            else
            {
                retry;
            }
        }
        catch
        {
            if (xSession::currentRetryCount() >= #RetryNum)
            {
                throw error(strFmt("@MAZ:FailedToDeleteDataFromTmpTable", 'HMClaimQualifiedForPoolAssignment'));
            }
            else
            {
                retry;
            }
        }

        try
        {
            if(this.isInBatch())
            {
                numberOfThreads = numberOfThreads == 0 ? 1 : numberOfThreads < 0 ? numberOfThreads * -1 : numberOfThreads;

                recordsToProcess = SysQuery::countLoops(gQueryRun);
                
                Info(strFmt("@MAZ:NoOfRecordsPicked", recordsToProcess));

                if(recordsToProcess <= numberOfThreads)
                {
                    numberOfThreads = 1;

                    Info("@MAZ:RecordsLesserThanNumberOfThreads");
                }

                perThreadRecords        = recordsToProcess / numberOfThreads;
                perThreadRecordsRound   = perThreadRecords;
                diffPerThread           = perThreadRecords - perThreadRecordsRound;
                additionalRecords       = diffPerThread * numberOfThreads;
                additionalRecords       = roundUp(additionalRecords, 1);
                additionalRecordsRound  = additionalRecords;

                if(!batchHeader)
                {
                    batchJobId = this.parmCurrentBatch().BatchJobId;

                    batchHeader = BatchHeader::construct(batchJobId);

                    batchHeader.parmRetriesOnFailure(5);
                }

                hmClaimBatchJobLogsHeaderDataPrep = HMClaimBatchJobLogsHeader::insertData(HMClaimJobType::PoolZeroBalanceDataPrep
                                                                                            , batchJobId
                                                                                            , recordsToProcess
                                                                                            , numberOfThreads);

                hmClaimBatchJobLogsHeader = HMClaimBatchJobLogsHeader::insertData(HMClaimJobType::PoolZeroBalance
                                                                                    , batchJobId
                                                                                    , recordsToProcess
                                                                                    , numberOfThreads);

                if(perThreadRecordsRound + additionalRecordsRound > 0)
                {
                    
                    hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob = HMDataPrepForCalcZeroClaimPoolMultiThreadBatchJob::construct();
                    hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob.parmQueryRun(gQueryRun);
                    hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob.parmNumberOfThreads(numberOfThreads);
                    hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob.parmPerThreadRecordsRound(perThreadRecordsRound);
                    hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob.parmAdditionalRecords(additionalRecordsRound);
                    hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob.parmTaskDescription("@Optum:DataPreparationForZeroClaimPool");
                    hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob.parmHMClaimBatchJobLogsHeader(hmClaimBatchJobLogsHeaderDataPrep);

                    hmCalculateZeroClaimPoolMultiThreadBatchJob = HMCalculateZeroClaimPoolMultiThreadBatchJob::construct();
                    hmCalculateZeroClaimPoolMultiThreadBatchJob.parmNumberOfThreads(numberOfThreads);
                    hmCalculateZeroClaimPoolMultiThreadBatchJob.parmTaskDescription("@Optum:AssignmentOfPoolsOnZeroClaims");
                    hmCalculateZeroClaimPoolMultiThreadBatchJob.parmHMClaimBatchJobLogsHeaderDataPrep(hmClaimBatchJobLogsHeaderDataPrep);
                    hmCalculateZeroClaimPoolMultiThreadBatchJob.parmHMClaimBatchJobLogsHeader(hmClaimBatchJobLogsHeader);

                    hmUpdateZeroClaimBatchJobLogsHeaderCount = HMUpdateZeroClaimBatchJobLogsHeaderCount::construct();

                    jobType         = HMClaimJobType::PoolZeroBalance;
                    jobTypeDataPrep = HMClaimJobType::PoolZeroBalanceDataPrep;

                    hmUpdateZeroClaimBatchJobLogsHeaderCount.parmJobType(jobType);
                    hmUpdateZeroClaimBatchJobLogsHeaderCount.parmJobTypeDataPrep(jobTypeDataPrep);
                    hmUpdateZeroClaimBatchJobLogsHeaderCount.parmHMClaimBatchJobLogsHeaderDataPrep(hmClaimBatchJobLogsHeaderDataPrep);
                    hmUpdateZeroClaimBatchJobLogsHeaderCount.parmHMClaimBatchJobLogsHeader(hmClaimBatchJobLogsHeader);

                    batchHeader.addRuntimeTask(hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob, this.parmCurrentBatch().RecId);
                    batchHeader.addRuntimeTask(hmCalculateZeroClaimPoolMultiThreadBatchJob, this.parmCurrentBatch().RecId);
                    batchHeader.addRuntimeTask(hmUpdateZeroClaimBatchJobLogsHeaderCount, this.parmCurrentBatch().RecId);

                    batchHeader.addDependency(hmCalculateZeroClaimPoolMultiThreadBatchJob, hmDataPrepForCalcZeroClaimPoolMultiThreadBatchJob);
                    batchHeader.addDependency(hmUpdateZeroClaimBatchJobLogsHeaderCount, hmCalculateZeroClaimPoolMultiThreadBatchJob);
                }
                else
                {
                    Info("@MAZ:NoRecordsFoundForProcessing");
                }

            }

            if(batchHeader)
            {
                // save the batchheader with added tasks
                batchHeader.save();
            }

        }
        catch
        {
            throw error(strFmt("@MAZ:BatchJobInitiationFailed",xSession::xppCallStack()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    //Stores parameters of the batch
    public container pack()
    {
        container pack = conNull();

        if (gQueryRun)
        {
            pack = gQueryRun.pack();
        }

        return [#CurrentVersion] + [pack] + [#CurrentList] + [#HMClaimBatchJobLogsHeader] + [#HMClaimBatchJobLogsHeaderDataPrep] + [#JobType] + [#JobTypeDataPrep];
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    //Returns the stored object for the batch to use
    public boolean unpack(container packedClass)
    {
        boolean                 ret         = false;
        int                     version     = RunBase::getVersion(packedClass);
        container               packedQuery = conNull();

        switch (version)
        {
            case #CurrentVersion:
                [version, packedQuery, #CurrentList, #HMClaimBatchJobLogsHeader, #HMClaimBatchJobLogsHeaderDataPrep, #JobType, #JobTypeDataPrep] = packedClass;
                this.initQueryRun();
                ret         = true;
                break;

            default:
                ret = false;
        }
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// Describes whether the class is designed for execution in a new session.
    /// </summary>
    /// <returns>
    /// false.
    /// </returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    public QueryRun queryRun()
    {
        return gQueryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmQueryRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the gQueryRun object
    /// </summary>
    /// <param name="_qr"><c>QueryRun</c> object</param>
    public void parmQueryRun(QueryRun _qr)
    {
        gQueryRun = _qr;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQueryRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Defined the batch job query
    /// </summary>
    public void initQueryRun()
    {
        Query                query;
        QueryBuildDataSource qbds;
        
        query = new Query (queryStr(HMCalculateZeroClaimPoolQuery));

        qbds = query.dataSourceTable(tableNum(HMClaimTable));
        qbds.addRange(fieldNum(HMClaimTable, RecId)).value(strFmt('(%1.%2 == 1) || (%1.%3 == 0)',
                                                            qbds.name(),
                                                            fieldStr(HMClaimTable, IsClaimModified),
                                                            fieldStr(HMClaimTable, ClaimPool)));

        gQueryRun = new QueryRun(query);
        gQueryRun.saveUserSetup(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParmDefault</Name>
				<Source><![CDATA[
    /// <summary>
    /// Intialize the Query run to the parameter
    /// </summary>
    public void initParmDefault()
    {
        this.initQueryRun();

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmNumberOfThreads</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the numberOfThreads value
    /// </summary>
    /// <param name="_numberOfThreads"><c>HMNumberOfThreads</c> value</param>
    public void parmNumberOfThreads(HMNumberOfThreads _numberOfThreads)
    {
        numberOfThreads = _numberOfThreads;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Wrappable(true), Replaceable(true)]
    public boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>